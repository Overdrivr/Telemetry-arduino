plugins {
  id "org.ajoberstar.grgit" version "1.5.0-milestone.2"
}

import org.ajoberstar.grgit.*

task cloneCoreRepo << {
  delete "${rootDir}/build/repo"
  delete "${rootDir}/src/core"
  Grgit.clone(dir: file('build/repo'), uri: 'https://github.com/Overdrivr/Telemetry')
}

task copyTestFile(type: Copy) {
  from './test/data/'
  into './build/install/test/debug/'
  include '*.csv'
}

task prepare(type: Copy) {
  dependsOn << cloneCoreRepo
  dependsOn << copyTestFile
  from './build/repo/src/telemetry/c', './build/repo/src/telemetry/headers/'
  into './src/core/'
  include '**/**.c','**/**.h'
}

task distribute(type: Zip){
  dependsOn << prepare
  from './src/'
  into './Telemetry/'
  archiveName 'dist/Telemetry-arduino.zip'
}

apply plugin: 'cpp'

model {
  buildTypes {
    debug
    release
  }

  toolChains {
    gcc(Gcc) { }
  }

  components {
    hardwareSerialMock(NativeLibrarySpec) {
      sources {
        cpp {
          source {
             srcDirs "test/hardwareserial/"
             include "HardwareSerial.cpp"
          }
          exportedHeaders {
            srcDirs "test/hardwareserial/"
            include "HardwareSerial.h"
          }
        }
      }
    }

    telemetry(NativeLibrarySpec) {
      binaries.all {
        lib library: "hardwareSerialMock"
      }
      sources {
        cpp {
          source {
             srcDirs "src/"
             include "**/*.c", "**/*.cpp"
          }
          exportedHeaders {
            srcDirs "src/"
            include "**/*.h", "**/*.hpp"
          }
        }
      }
    }

    test(NativeExecutableSpec) {
      binaries.all {
        lib library: "hardwareSerialMock"
        lib library: "telemetry"
      }
      sources {
        cpp {
          source {
             srcDirs "test/src/"
             include "**/*.cpp"
          }
          exportedHeaders {
            srcDirs "test/src/", "test/data/"
            include "**/*.hpp", "**/*.csv"
          }
        }
      }
    }

  }
}
